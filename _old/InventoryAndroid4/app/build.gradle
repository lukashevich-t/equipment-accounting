apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion project.androidTargetSdk
    buildToolsVersion project.androidBuildTools

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion project.androidTargetSdk
        versionCode 200
        versionName "2.0.0"
//        multiDexEnabled = true
    }

    def validConfig
    def keystoreFile
    def keystorePassword
    def keystoreAlias

    try {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        keystoreFile = properties.getProperty('keystore.file')
        keystorePassword = properties.getProperty('keystore.password')
        keystoreAlias = properties.getProperty('keystore.alias')
        validConfig = keystoreFile != null && keystorePassword != null && keystoreAlias != null;
    } catch (error) {
        validConfig = false;
    }

    if (validConfig) {
        System.out.println("Release signing configured with " + keystoreFile)
        signingConfigs {
            release {
                storeFile project.rootProject.file(keystoreFile)
                storePassword keystorePassword
                keyAlias keystoreAlias
                keyPassword keystorePassword
            }
        }
    } else {
        System.out.println("Specify keystore.file, keystore.alias and keystore.password in local.properties to enable release signing.")
    }

    buildTypes {
        release {
            if (validConfig) {
                signingConfig signingConfigs.release
            }

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}


dependencies {
    // If you use this from an external project, use the following instead:
    //   compile 'com.journeyapps:zxing-android-embedded:<version>'
    implementation project(':zxing-android-embedded')
    implementation project.zxingCore
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // leakcanary is for development purposes only
    // https://github.com/square/leakcanary
    implementation 'com.android.support:appcompat-v7:25.4.0'
    implementation 'com.android.support:support-v13:25.4.0'

    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.android.support:design:25.4.0'
    implementation 'com.android.support:support-v4:25.4.0'
    implementation 'com.android.support:support-vector-drawable:25.4.0'
    testImplementation 'junit:junit:4.12'

    implementation "org.jetbrains.anko:anko-sdk25:$anko_version"
    implementation "org.jetbrains.anko:anko-sdk25-coroutines:$anko_version"
    implementation "org.jetbrains.anko:anko-appcompat-v7:$anko_version"
    implementation "org.jetbrains.anko:anko-sqlite:$anko_version"
//    implementation "org.jetbrains.anko:anko-design:$anko_version"
    implementation 'com.github.kittinunf.fuel:fuel-android:1.12.1'
    implementation 'com.google.code.gson:gson:2.8.2'


//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5'
//    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
}
kotlin {
    experimental {
        coroutines "enable"
    }
}
